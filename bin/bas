.key arg1,arg2,arg3

; ** Preprocess, compile, assemble and link an ACE program. **
; ** Uses YAP preprocessor, vasm assembler, vlink linker. **
;
; Usage:
;   bas sourcefile                      - Compile sourcefile.b
;   bas sourcefile.b                    - Also works (strips .b)
;   bas sourcefile extra.o              - Compile and link with extra.o
;   bas sourcefile a.o,b.o,c.o          - Link with multiple extra objects
;   bas -option sourcefile              - Compile with ace options
;   bas -option sourcefile extra.o      - Both options and extra link
;   bas -S sourcefile                   - Compile to assembly only (.s)
;   bas -m modulefile                   - Compile module to object file (.o)
;
; The script also parses REM #using directives in the first 20 lines:
;   REM #using ace:submods/list/list.o  - Adds list.o to the link stage

; Command paths
ALIAS cmd_ace ace:bin/ace
ALIAS cmd_yap ace:bin/yap
ALIAS cmd_vasm ace:bin/vasmm68k_mot
ALIAS cmd_vlink ace:bin/vlink

; make sure stack is big enough.
STACK 65000

; don't abort script if ACE or YAP quits
; with a return code of 10 (ERROR).
FAILAT 11

IF "<arg1>" EQ ""
  echo "Usage: bas [-options] sourcefile[.b] [extra.o]"
  SKIP end
ENDIF

; Check if first argument starts with "-" (is an ace option)
rx "if left('<arg1>', 1) = '-' then say 1; else say 0" >T:bas_opt
SET hasopt `type T:bas_opt`
delete >NIL: T:bas_opt

; Set up variables based on whether we have options
IF $hasopt EQ 1
  SET aceopts "<arg1>"
  SET srcarg "<arg2>"
  SET extraobj "<arg3>"
  IF "<arg2>" EQ ""
    echo "Error: source file required after options"
    SKIP end
  ENDIF
ELSE
  SET aceopts ""
  SET srcarg "<arg1>"
  SET extraobj "<arg2>"
ENDIF

; Convert comma-separated extra objects to space-separated
IF NOT "$extraobj" EQ ""
  rx "say translate('$extraobj', ' ', ',')" >T:bas_extra
  SET extraobj `type T:bas_extra`
  delete >NIL: T:bas_extra
ENDIF

; Handle .b extension - determine base name
SET srcbase "$srcarg"
IF EXISTS "$srcarg"
  IF NOT EXISTS "$srcarg.b"
    ; File exists without adding .b - user passed full name with .b
    ; Strip .b extension using rx (ARexx)
    rx "say substr('$srcarg', 1, length('$srcarg')-2)" >T:bas_base
    SET srcbase `type T:bas_base`
    delete >NIL: T:bas_base
  ENDIF
ENDIF

; Scan for REM #using directives in first 20 lines
SET usingobj ""
rx ace:bin/parseUsing.rexx $srcbase.b >T:bas_using
SET usingobj `type T:bas_using`
delete >NIL: T:bas_using

; Append #using objects to extraobj
IF NOT "$usingobj" EQ ""
  echo "Found #using: $usingobj"
  SET extraobj "$extraobj $usingobj"
ENDIF

; preprocess source file.
echo "Preprocessing..."
cmd_yap $srcbase.b T:$srcbase.b
IF ERROR
  SKIP end
ENDIF
echo "...Done"

; Check if assembly-only compilation (-S flag)
rx "if pos('S', '$aceopts') > 0 then say 1; else say 0" >T:bas_isasm
SET isasm `type T:bas_isasm`
IF $isasm EQ 1
    ; remove -S flag to not pass it to ace
    SET aceopts ""
ENDIF
delete >NIL: T:bas_isasm

; compile preprocessed source file.
echo "Compiling with ace $aceopts T:$srcbase ..."
IF "$aceopts" EQ ""
  cmd_ace T:$srcbase
ELSE
  cmd_ace $aceopts T:$srcbase
ENDIF
echo "done"

IF ERROR
  SKIP end
ENDIF

IF $isasm EQ 1
  ; Assembly only: copy .s file and stop
  delete >NIL: T:$srcbase.b
  copy T:$srcbase.s ""
  delete >NIL: T:$srcbase.s
  SKIP end
ENDIF

; assemble
delete >NIL: T:$srcbase.b
cmd_vasm -Fhunk -o T:$srcbase.o T:$srcbase.s
delete >NIL: T:$srcbase.s

; Check if module compilation (-m flag anywhere in options)
rx "if pos('m', '$aceopts') > 0 then say 1; else say 0" >T:bas_ismod
SET ismod `type T:bas_ismod`
delete >NIL: T:bas_ismod

IF $ismod EQ 1
  ; Module: just copy the .o file, don't link
  copy T:$srcbase.o ""
  delete >NIL: T:$srcbase.o
ELSE
  ; Normal: link to executable
  cmd_vlink -bamigahunk -x -Bstatic -nostdlib -mrel -s -o T:$srcbase T:$srcbase.o $extraobj ACElib:startup.lib ACElib:db.lib ACElib:ami.lib

  ; leave us with the executable (and icon?).
  copy T:$srcbase ""

  IF EXISTS T:$srcbase.info
    copy T:$srcbase.info ""
  ENDIF

  ; kill any remaining temporary files.
  delete >NIL: T:$srcbase#?
ENDIF

LAB end
